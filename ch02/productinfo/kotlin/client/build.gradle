buildscript {
    ext.kotlin_version = '1.6.10'
    ext.coroutines_version = '1.5.0'
    ext.protobuf_version = '3.19.1'
    ext.grpc_version = '1.42.0' // CURRENT_GRPC_VERSION
    ext.grpc_kotlin_version = '1.2.0' // CURRENT_GRPC_KOTLIN_VERSION
    ext.protobuf_gradle_plugin_version = '0.8.19'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:${protobuf_gradle_plugin_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'
apply plugin: 'application'

repositories {
    google()
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation "io.grpc:grpc-kotlin-stub:${grpc_kotlin_version}"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutines_version}"

    // gRPC and Protobuf
    implementation "com.google.protobuf:protobuf-gradle-plugin:${protobuf_gradle_plugin_version}"
    implementation "com.google.protobuf:protobuf-java:${protobuf_version}"
    implementation "com.google.protobuf:protobuf-java-util:${protobuf_version}"
    implementation "com.google.protobuf:protobuf-kotlin:${protobuf_version}"
    implementation "io.grpc:grpc-netty-shaded:${grpc_version}"
    implementation "io.grpc:grpc-protobuf:${grpc_version}"
    implementation "io.grpc:grpc-stub:${grpc_version}"

    compileOnly "javax.annotation:javax.annotation-api:1.2"

    // Google
    implementation "com.google.guava:guava:28.2-jre"
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protobuf_version}" }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:${grpc_kotlin_version}:jdk7@jar"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                // Generate Java gRPC classes
                grpc { }
                // Generate Kotlin gRPC using the custom plugin from library
                grpckt { }
                kotlin { }
            }
        }
    }
}

startScripts.enabled = false

task productInfoClient(type: CreateStartScripts) {
    mainClassName = 'ecommerce.ProductInfoClientKt'
    applicationName = 'productinfo-client'
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(productInfoClient)
    fileMode = 0755
}